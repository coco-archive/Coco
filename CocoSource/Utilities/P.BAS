5 GOSUB 10:GOTO 240
10 CLS:PRINT"PUNCH CARD"
15 PRINT
20 PRINT"ENTER DATE AND TIME:"
30 PRINT"MM/DD/YYYY HH:MM:SS LOG"
40 LINE INPUT A$
60 B$=MID$(A$,3,1)+MID$(A$,6,1)+MID$(A$,11,1)+MID$(A$,14,1)+MID$(A$,17,1)+MID$(A$,20,4)
70 IF (B$<>"// :: ON") AND (B$<>"// :: OFF") THEN 15
80 MM=VAL(A$)
90 DD=VAL(MID$(A$,4,2))
100 YY=VAL(MID$(A$,7,4))
110 HH=VAL(MID$(A$,12,2))
120 MI=VAL(MID$(A$,15,2))
130 SS=VAL(MID$(A$,18,2))
140 LO$=MID$(A$,21,3)
150 IF YY<>1989 THEN 15
155 YY=YY-1980
160 IF DD>31 OR DD<1 THEN 15
170 IF MM>12 OR MM<1 THEN 15
180 IF HH>23 OR HH<0 THEN 15
190 IF MI>59 OR MI<0 THEN 15
200 IF SS>59 OR SS<0 THEN 15
201 IF LO$="ON" THEN LO=0 :ELSE IF LO$="OFF" THEN LO=1 :ELSE 15
220 :'YY MM DD  HH MM SS LO
230 PRINTYY;MM;DD;HH;MI;SS;LO
235 RETURN
240 OPEN "R",#1,"PUNCH.LOG",8:FIELD 1,8 AS L$
250 A$=CHR$(YY)+CHR$(MM)+CHR$(DD)+CHR$(HH)+CHR$(MI)+CHR$(SS)+CHR$(LO)+CHR$(0)
260 GET #1,1:R=CVN(L$):R=R+1:LSET L$=MKN$(R):PUT #1,1:LSET L$=A$:PUT #1,R:CLOSE
270 PRINT "YOU ARE NOW LOGGED ";LO$
280 END
1000 KT=0:DIM A(8):OPEN "R",#1,"PUNCH.LOG",8
1010 FIELD 1,8 AS L$
1020 GET #1,1:R=CVN(L$)
1030 FOR X=2 TO R:GET #1,X:PRINT STR$(X-1);":";:FOR Y=1 TO 6:A(Y)=ASC(MID$(L$,Y,1)):PRINTSTR$(A(Y));:NEXT Y:LT=T:T=A(6)+A(5)*60+A(4)*60*60+A(3)*60*60*24+A(2)*60*60*24*31
1031 IF MID$(L$,7,1)=CHR$(0) THEN PRINT " ON" :ELSE PRINT " OFF ";:T=INT( (T-LT)/3600 * 100)/100:KT=KT+T:PRINT STR$(T)
1032 NEXT X:CLOSE:PRINT:PRINT"TOTAL ";KT:END
2010 INPUT"HIT ENTER TO DESTROY PUNCH FILE";A:OPEN "R",#1,"PUNCH.LOG",8:FIELD 1,8 AS L$:LSET L$=MKN$(1):PUT #1,1:CLOSE:END
3000 GOSUB 10
3010 INPUT "WHICH RECORD";R:R=R+1
3020 INPUT "HIT ENTER TO VERIFY";A
3030 OPEN "R",#1,"PUNCH.LOG",8
3040 FIELD 1,8 AS L$
3050 A$=CHR$(YY)+CHR$(MM)+CHR$(DD)+CHR$(HH)+CHR$(MI)+CHR$(SS)+CHR$(LO)+CHR$(0)
3060 LSET L$=A$:PUT #1,R:CLOSE
3070 PRINT"YOU HAVE CHANGED THE LOG":CLOSE:END
