       ORG   $E00

PRINT  PSHS  X,D
       LDX   +4,S
       LDA   ,X+
A@     JSR   [$A002]
       LDA   ,X+
       BNE   A@
       STX   +4,S
       PULS  X,D,PC

BLOCK  FCB   8
BLAT   FDB   $6002
LENGTH FDB   0
WRITE
       PSHS  X,Y,U,D
       LDB   $FFA3
       PSHS  B
       LDB   BLOCK
       STB   $FFA3
       LDX   BLAT
       STA   ,X+
       CMPX  #$8000
       BNE   A@
       LDX   #$6000
       INC   BLOCK
A@     STX   BLAT
       LDD   LENGTH
       ADDD  #1
       STD   LENGTH
       PULS  B
       STB   $FFA3
       PULS  X,Y,U,D,PC

LONGJMP FDB 0
BLOCKIT FDB $0102
TOTLEN FDB   0
LASLEN FDB   $4000
ADJUSTY
       PSHS  X,U,D
       TFR   Y,D
       SUBD  LASLEN
       ADDD  TOTLEN
       STD   TOTLEN
       CMPD  #$8000
       BHI   O@
       CMPY  #$7000
       BLO   Z@
       LDD   BLOCKIT
       ADDD  #$0101
       STD   BLOCKIT
       STD   $FFA2
       LEAY  -$2000,Y
Z@     STY   LASLEN
       PULS  X,U,D,PC
*WE ARE DONE - SEND GOOD MESSAGE
O@     LDD   #$0809
       STD   $FFA2
       LDD   LENGTH
       STD   $4000
       LDS   LONGJMP
       RTS

MATCH  PSHS  X,Y,U,D
       NEGA
       LEAU  A,Y
       NEGA
A@     LDB   ,Y+
       CMPB  ,U+
       BNE   Z@
       DECA
       BNE   A@
Z@     PULS  X,Y,U,D,PC

EXEC   STS   LONGJMP

       LDY   #$4000
       LDD   #$0102
       STD   $FFA2

A@     JSR   ADJUSTY
*ZEROS HAVE BEEN COMPRESSED
       CMPY  #$4010
       BLO   NC@ *NO COMPRESS
*LETS GIVE IT A TRY
*DO THE LAST X MATCH THE NEXT X?
       LDA   #16 *# TO MATCH
MA@    JSR   MATCH
       BNE   MN@ *NO MATCH
*WE HAVE A MATCH!!!!!!!!!!!!
       LEAY  A,Y
       PSHS  A
       ADDA  #'0
       JSR   [$A002]
       PULS  A
       ORA   #%11000000
       JSR   WRITE
       BRA   A@
MN@    DECA
       CMPA  #2
       BHS   MA@
*NO MATCH FOUND AT ALL
NC@    LDD   ,Y
       BNE   GO@
*WE HAVE AT LEAST 2 ZEROS
Z@     LEAY  +2,Y *SKIP THEM
       LDA   #$FF *1 ZERO
B@     INCA  *2 OR MORE ZEROS
       JSR   ADJUSTY
       LDB   ,Y+
       BEQ   B@
       LEAY  -1,Y
       ORA   #%01000000
       JSR   WRITE
       BRA   A@
GO@    LDA   ,Y+
       JSR   WRITE
       BRA   A@




       END   EXEC
