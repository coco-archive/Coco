1 CMP
10 GOTO 10000
13 GOTO 15
14 PCLEAR 1:GOTO 13
15 CLEAR700,&H4CA0:LOADM"TOY":LOADM"WINDOW":WIDTH 32:CLS:PRINT"WHICH SPEED UP POKE?":PRINT"A) COCO 3":PRINT"B) COCO 2":PRINT"C)NONE":INPUT A$:IF A$="A" THEN QS=65496:QF=65497 :ELSE IF A$="B"THEN QS=65494:QF=65495 :ELSE QS=65494:QF=65494
16 POKEQF,0:CLS2:PRINT@72,"*** MUSIC+ *** ";:PRINT@128+14,"BY";:PRINT@160+12,"RAINBOW";:PRINT@197,"*** COCO COMPOSING***";
17 PRINT@231," RAINBOW DEC 1983 ";:P7$="L50;V5;A":POKEQF,0
18 A5=&H4F22:A1=&H4D00:A2=&H4E83:A3=&H4E84:A4=&H4F21:A6=&H4CD7:A7=&H4CE2:A8=&H4ECD:A9=&H4EA0:DIMP(5,5):TT=17113:CM$="^"+CHR$(10)+CHR$(8)+CHR$(9)+"ZKPTSLJIRM "+CHR$(12)+"XY"
19 PRINT@353," PLEAS TURN TV VOLUME UP AND ";:PRINT@385," WAIT A FEW SECONDS......... ";
20 P$=CHR$(128):C$=CHR$(143):R$=CHR$(255):B$=CHR$(175)
21 X$="C C#D D#E F F#G G#A A#B "
22 DEF FN HI(X)=INT(X/256):DEF FN LO(X)=X-INT(X/256)*256
25 FOR A=1 TO 5:FOR B=1 TO 5:P(A,B)=Q:Q=Q+1:NEXTB,A
27 Q1$=MID$(QQ$,2,1):Q2$=MID$(QQ$,3,1):IF Q1$="P" OR Q2$="P" THEN 50
30 CP$=""
31 READ A$:IF A$<>"END" THEN CP$=CP$+CHR$(VAL("&H"+A$)):GOTO 31
42 FOR D=A6 TO A7:READP:POKED,P:NEXT
50 DEFUSR0=A3:DEFUSR1=A6
52 CLS2:PRINT@73," ** MUSIC+ ** ";:PRINT@137," ENHANCEMENTS ";:PRINT @205," BY ";:PRINT@266," BOB LUDLUM ";
53 PRINT @293," 226 PINE RIDGE DRIVE ";:PRINT@322," PANAMA CITY,FLORDA 32405 ";
54 FOR I=1TO200:NEXTI:PRINT@385," HERE'S A BIT OF 'TOY FOR TWO ";:PRINT@417," LUTES' FROM ROBINSON'S 'THE  ";:PRINT@449," SCHOOLE OF MUSICKE' (1603)   ";
55 IF Q1$="M" OR Q2$="M" THEN 56 :ELSE POKEQS,0:POKEQF,0:REM      MUSIC WAS PLAYED HERE
56 M=A5
57 PE=PEEK(M):IFPE=0THENJ=M-1:GOTO 60
58 M=M+5:GOTO 57
60 POKE QF,0:GOSUB500:Z=1:S=139:X=1:Y=1:M=A5
70 GOSUB 400:PRINT@S,P$;
100 PRINT@1,"HC ";INT((J-A5+5)/5);:PRINT@65,"CL";M;
101 I$=INKEY$:IF I$="" THEN 101
102 IF I$="@" THEN GOSUB 9000 :ELSE ON INSTR(1,CM$,I$) GOTO 250,280,320,350,9400,9200,600,9100,625,700,900,930,950,9300,136,60,9500,9600
103 GOTO 101
136 PRINT@S,R$;:IF X=1 THEN GOSUB 7000 :ELSE GOSUB 8000
140 IF TV=94 THEN 250 :ELSE IF TV=10 THEN 280 :ELSE IF TV=8 THEN 320 :ELSE IF TV=9 THEN 350
141 GOTO 100
250 IF X=1 THEN 100
255 X=X-1:PRINT@S,C$;:IF X=1 THEN S=S-64 :ELSES=S-32
270 PRINT@S,P$;:M=M-1
272 GOTO 100
280 IF X=5 THEN100
285 X=X+1:PRINT@S,C$;:IFX=2THEN S=S+64 :ELSE S=S+32
300 PRINT@S,P$;:M=M+1:IF M>J THEN J=M:PRINT@33,"HM";J;
310 GOTO 100
320 IFY=1 AND M<A5+5 THEN 100
325 IF Y=1 THEN 340
330 Y=Y-1:PRINT@S,C$;:S=S-4:PRINT@S,P$;:M=M-5
335 GOTO 100
340 Z=Z-1:M=M-(P(Y,X)+5):SS=S:S=139:GOSUB400
345 S=SS:PRINT@S,P$;:M=M+P(Y,X)
347 GOTO 100
350 IFY=5ANDM>32760THEN100
355 IFY=5THEN380
360 Y=Y+1:PRINT@S,C$;:S=S+4:PRINT@S,P$;:M=M+5:IF M>J THEN J=M:PRINT@33,"HM";J;
370 GOTO 100
380 Z=Z+1:M=M-(P(Y,X)-5):SS=S:S=139:GOSUB400
385 S=SS:PRINT@S,P$;:M=M+P(Y,X):IF M>J THEN J=M:PRINT@33,"HM";J;
390 GOTO100
400 PRINT@33,"HM";J;:PRINT@75,"                  ";
402 PRINT@75,RIGHT$(STR$(Z),3)" ";:PRINT@79,RIGHT$(STR$(Z+1),3)" ";:PRINT@83,RIGHT$(STR$(Z+2),3)" ";:PRINT@87,RIGHT$(STR$(Z+3),3)" ";:PRINT@91,RIGHT$(STR$(Z+4),3)" ";:PRINT@95,B$;
410 POKE &H7800,M/256:POKE &H7801,M-256*PEEK(&H7800):EXEC &H7802
420 RETURN
500 CLS3:PRINT@12," * ENTER MUSIC * ";:B7$="        ":PRINT@1,B7$;:PRINT@33,B7$;:PRINT@65,B7$;
502 T9=256*PEEK(A8)+PEEK(A8+1):T9=TT/T9:PRINT@489," TEMPO=     ";:PRINT@497,USING"###";T9;
505 PRINT@44,"   (SPACE BAR)   ";
510 PRINT@129," LENGTH  ";
520 PRINT@193," VOICE 1 ";
530 PRINT@225," VOICE 2 ";
540 PRINT@257," VOICE 3 ";
550 PRINT@289," VOICE 4 ";
560 PRINT@353," CURSOR(ARROWS) : MENU(@ KEY) ";
570 MF=1:GOSUB9000
580 RETURN
600 CLS2:PRINT@230," MUSIC PERFORMANCE ";:POKEQS,0:POKEA9,0:POKEA9+1,128:C=USR0(0):GOTO60
625 POKEQS,0:CLS8:PRINT@9," *SAVE MUSIC* ";:GOSUB800
630 IFT=1THENPRINT@8," *SAVE TO DISK* ";:ELSEPRINT@7,"*SAVE TO CASSETTE*";
660 PRINT@96," 8 OR LESS CHARACTERS"
665 INPUTT$
675 FORQ=J+1TOJ+5:POKEQ,0:NEXTQ
680 IFT=1THEN681:ELSEIFT=-1THEN682:ELSE60
681 SAVEMT$,A1,Q,A3:GOTO60
682 CSAVEMT$,A1,Q,A3:GOTO60
685 GOTO60
700 POKEQS,0:CLS4:PRINT@9," *LOAD MUSIC* ";:GOSUB800
710 IFT=1THENPRINT@7," *LOAD FROM DISK* ";:ELSEPRINT@5," *LOAD FROM CASETTE* ";
735 PRINT@96," (MUST BE EXACT) "
740 INPUTT$
745 PRINT@199," WAIT ONE MOMENT ";
750 IFT=1THENLOADMT$:ELSECLOADMT$
760 M=A5
761 PE=PEEK(M):IFPE=0THENJ=M-1:GOTO60
765 M=M+5:GOTO761
800 PRINT@64," SELECT (D)ISK OR (C)ASSETTE":PRINT@96,"   PRESS SPACE BAR TO RETURN"
810 I$=INKEY$:IFI$=""THEN810
820 IFI$="D"THENT=1:GOTO850
835 IFI$="C"THENT=-1:GOTO850
840 IFI$=" "THEN60:ELSE810
850 PRINT@64," ENTER TITLE:":RETURN
900 PRINT@44," JUMP TO COLOMN ";
905 PRINT@12,R$" SELECT #       "R$;:PRINT@22,;:INPUTZ:PRINT@29,B$+B$+B$;:IFZ<1ORZ>2350THEN905
910 S=139:X=1:Y=1:M=(Z-1)*5+A5
920 IF M>J THEN J=M
925 PRINT@12," * ENTER MUSIC * ";B$+B$+B$;:PRINT@44,"   (SPACE BAR)   ";:GOTO70
930 PRINT@S,R$+R$+R$+R$;:M=M-(X-1):J=J+5:IF J-M < 1 THEN 100
931 :'MOVE M TO M+5 (LEN=J-M)
932 MID$(CP$,1,6)=CHR$(FN HI(M))+CHR$(FN LO(M))+CHR$(FN HI(J-M))+CHR$(FN LO(J-M))+CHR$(FN HI(M+5))+CHR$(FN LO(M+5))
933 VP=1:VP=VARPTR(CP$):EXEC PEEK(VP+2)*256+PEEK(VP+3)+6
934 FOR K=M TO M+4:POKE K,0:NEXT K
935 S=S-(X-1)*32:IFX>1THENS=S-32
940 X=1:M=M-P(Y,X):SS=S:S=139:GOSUB400
945 M=M+P(Y,X):S=SS:PRINT@S,P$;:GOTO100
950 IF M<(A5+5) AND J<(A5+5) THEN 100 :ELSE PRINT @S,R$+R$+R$+R$;:M=M-(X-1):IF J-M < 1 THEN 100
951 :'MOVE M+5 TO M LENGTH=J-M
952 MID$(CP$,1,6)=CHR$(FN HI(M+5))+CHR$(FN LO(M+5))+CHR$(FN HI(J-M))+CHR$(FN LO(J-M))+CHR$(FN HI(M))+CHR$(FN LO(M)):J=J-5
955 VP=1:VP=VARPTR(CP$):EXEC PEEK(VP+2)*256+PEEK(VP+3)+6
960 S=S-(X-1)*32:IFX>1THENS=S-32
970 X=1:M=M-P(Y,X):SS=S:S=139:GOSUB400
980 M=M+P(Y,X):S=SS:PRINT@S,P$;:GOTO100
6000 DATA 0,0,0,0,0,0,20,0,10,AE,8C,F6,EE,8C,F1,AE,8C,F2,1F,30,A3,8C,ED,25,9,A6,C0,A7,80,31,3F,26,F8,39,EC,8C,DD,33,CB,30,8B,A6,C2,A7,82,31,3F,26,F8,39,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,FF,0,END
6040 :'*CLEAR MUSIC MEMORY
6050 DATA 142,79,34,79,167,128,140,111,255,38,249,57
6499 :'*CONVERT NOTE TO STRING
6999 :'*LENGTH CALCULATION
7000 I$=INKEY$:IFI$=""THEN7000:ELSETV=ASC(I$)
7010 IFTV=8ORTV=9ORTV=10ORTV=94THEN7160
7020 IFTV<48ORTV>57THEN7000
7030 LL=96:I1$=I$:I2$="":I3$="":PRINT@S,"    ";:PRINT@S+1,I1$;:N=VAL(I$):IFN=0THEN7150
7040 I$=INKEY$:IFI$=""THEN7040:ELSETV=ASC(I$)
7050 IFTV=8ORTV=9ORTV=10ORTV=94THEN7130
7060 IFTV=46THEN7120
7070 IFTV<48ORTV>57THEN7040
7080 I2$=I$:PRINT@S+1,I1$+I2$;:N=10*N+VAL(I$)
7090 I$=INKEY$:IFI$=""THEN7090:ELSETV=ASC(I$)
7100 IFTV=8ORTV=9ORTV=10ORTV=94THEN7130
7110 IFTV<>46THEN7090
7120 I3$=".":LL=144
7130 IFN<1ORN>96THENN=0:GOTO7150
7140 N=LL/N
7150 POKEM,N:PRINT@S+1,I1$+I2$+I3$;
7160 PRINT@S,P$;:IFY=5THENPRINT@S+4,B$;
7180 RETURN
8000 I$=INKEY$:IFI$=""THEN8000:ELSETV=ASC(I$)
8010 IFTV=8ORTV=9ORTV=10ORTV=94THEN8180
8020 N$=I$:IFI$="0"THENN=0:PRINT@S,"    ";:GOTO8160
8030 IFTV<65ORTV>71THEN8000
8040 PRINT@S,"    ";:PRINT@S+1,N$;
8042 IFI$="C"THEN N1=-6
8044 IFI$="D"THENN1=-2
8046 IFI$="E"THENN1=2
8048 IFI$="F"THENN1=4
8050 IFI$="G"THENN1=8
8052 IFI$="A"THENN1=12
8054 IFI$="B"THENN1=16
8060 I$=INKEY$:IFI$=""THEN8060:ELSETV=ASC(I$)
8070 IFTV<50ORTV>54THEN8060
8080 IF(N$="F"ORN$="G")ANDTV=54THEN8060
8100 N2=24*(VAL(I$)-2)+8:N=N1+N2:N$=N$+I$
8110 PRINT@S+1,N$;
8120 I$=INKEY$:IFI$=""THEN8120:ELSETV=ASC(I$)
8130 IFTV=8ORTV=9ORTV=10ORTV=94THEN8160
8140 IFI$="#"THENN=N+2:ELSEIFI$="-"THENN=N-2:ELSE8120
8150 N$=N$+I$
8160 PRINT@S,P$;:PRINT@S+1,N$;:IFY=5THENPRINT@S+4,B$;
8170 POKEM,N
8180 RETURN
8999 :'*ALTERNATE MENU
9000 MF=-1*MF
9010 IFMF=-1THEN9020:ELSE9030
9020 PRINT@417," (P)LAY ";:PRINT@428," (S)AVE ";:PRINT@439," (L)OAD ";:PRINT@449," (J)UMP ";:PRINT@460," (I)NSERT";:PRINT@471,"(R)EMOVE";:GOTO9040
9030 PRINT@417,"(T)EMPO ";:PRINT@428," (K)OPY ";:PRINT@439," (M)OVE ";:PRINT@449,"        ";:PRINT@460,"        ";:PRINT@471," (Z)ERO ";
9040 RETURN
9099 :'*SET TEMPO
9100 PRINT @489," TEMPO=? ";:TC=0
9105 T9$=INKEY$
9110 T9$=INKEY$:IF T9$="" THEN  9110
9115 T6=ASC(T9$):IF T6<48 OR T6>57 THEN 9110
9117 PLAY P7$
9120 IF TC=0 THEN TC=1:T9=100*VAL(T9$):PRINT@498,T9$;:GOTO 9110
9130 IF TC=1 THEN TC=2:T9=T9+10*VAL(T9$):PRINT@499,T9$;:GOTO 9110
9140 IF TC=2 THEN TC=0:T9=T9+VAL(T9$):PRINT @500,T9$;
9150 T9=INT(TT/T9):IF T9<18 THEN T9=18 :ELSE IF T9>600 THEN T9=600
9155 T8=INT(T9/256):T7=T9-256*T8
9160 POKE A8,T8:POKE A8+1,T7
9170 T9=TT/T9:PRINT@489," TEMPO=      ";:PRINT@497,USING"###";T9;:PRINT@488,B$;:PRINT@503,B$+B$;:GOTO 100
9199 :'* COPY
9200 PRINT@44,"  COPY (REPEAT)  ";:PRINT@10,R$"START COLUMN       "R$;:PRINT @23,;:INPUT CS:CS=A5+5*CS-5:PRINT@31,B$;
9210 PRINT@10,R$"END COLUMN       "R$;:PRINT @23,;:INPUT CE:CE=A5+5*CE-1:PRINT @31,B$;
9220 PRINT @10,R$"DEST. COLUMN       "R$;:PRINT @23,;:INPUT CT:CT=A5+5*CT-5:PRINT @31,B$;
9225 TA=CT
9230 FOR I=CS TO CE
9235 CV=PEEK(I):POKE TA,CV:TA=TA+1
9240 NEXT I
9250 S=139:X=1:Y=1:M=TA-5:Z=(M-A5+5)/5:M1=M+4:IF M1> J THEN J=M1
9260 PRINT @10,B$+B$;:PRINT@12,"* ENTER MUSIC* ";B$+B$+B$;:PRINT@44,"   (SPACE BAR)   ";:GOTO 70
9299 :'*MOVE PLAY START LOCATION
9300 PRINT @44,"BEGIN PLAY COLUMN";:PRINT @12,R$" SELECT#      "R$;:PRINT @ 21,;:INPUTZT:PRINT @29,B$+B$+B$;:ZT=INT(ZT):IF ZT<1 OR ZT>2300 THEN 9300 :ELSE ZT=128+5*(ZT-1)
9310 ZU=INT(ZT/4096):ZV=ZT-4096*ZU:ZW=INT(ZV/256):ZL=ZV-256*ZW:ZU=16*ZU+ZW
9320 POKE A9,ZU:POKE A9+1,ZL:CLS 2:PRINT @230," MUSIC PERFORMANCE ";: POKE QS,0:C=USR0(0):GOTO 60
9350 PRINT @12," * ENTER MUSIC * ";B$+B$+B$;:PRINT @44,"   (SPACE BAR)   ";:GOTO 70
9399 :'* CLEAR MUSIC MEM
9400 CLS:PRINT @96,"**** YOU ARE ABOUT TO CLEAR ****";:PRINT@128,"**** THE MUSIC DAD MEMORY ****";
9405 PRINT @224,"     IS THAT WHAT YOU WANT          TO DO (Y/N)";
9410 INPUT"";I$:IF I$="Y" THEN C=USR1(0)
9420 J=A5:GOTO 60
9500 PRINT@44,"  TRANSPOSE    ";:PRINT@10,R$"START COLUMN      ";R$;:PRINT @23,;:INPUT CS:CS=A5+5*CS-5:PRINT@31,B$;
9510 PRINT @10,R$"END COLUMN     "R$;:PRINT @23,;:INPUT CE:CE=A5+5*CE-1:PRINT @31,B$;
9521 PRINT @10,R$"HOW MUCH    ";R$;:PRINT @23,;:INPUT TR
9530 FOR I=CS TO CE STEP 5
9535 FOR I2=I+1 TO I+4
9540 CV=PEEK(I2):IF CV<>0 THEN CV=CV+TR*2:IF CV>=0 THEN POKE I2,CV
9560 NEXT I2,I
9570 GOTO 70
9600 PRINT@44,"  LENGTH CHANGE  ";:PRINT@10,R$"START COLUMN    ";R$;:PRINT @23,;:INPUT CS:CS=A5+5*CS-5:PRINT @31,B$;
9610 PRINT @10,R$"END COULUMN     ";R$;:PRINT @23,;:INPUT CE:CE=A5+5*CE-1:PRINT @31,B$;
9620 PRINT @10,R$"HOW MUCH      ";R$;:PRINT @23,;:INPUT TR
9630 FOR I=CS TO CE STEP 5
9640 CV=PEEK(I)*TR:IF CV<1 THEN POKE I,1 :ELSE POKE I,CV
9650 NEXT I
9670 GOTO 70
10000 PMODE0,1:PCLEAR1:GOTO14
10001 END
