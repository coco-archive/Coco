1 CMP
5 GOTO20000
10 CLS
14 CLEAR300,&H2FFF
20 INPUT"(C)ASETTE OR (D)ISK";D$:IF D$="C"THENCLOADM"PIANO":ELSELOADM"PIANO"
40 DIMN$(12)
50 CLS:PRINT@14,"MENU":PRINT
60 PRINT"  1) COMPOSE MUSIC"
70 PRINT"  2) ADD ON TO MUSIC"
80 PRINT"  3) PLAY MUSIC"
90 PRINT"  4) SAVE MUSIC"
100 PRINT"  5) LOAD MUSIC"
110 PRINT"  6) CHANGE ENVELOPE"
120 PRINT"  7) CHANGE TEMPO"
130 PRINT"  8) ASSEMBLE TO PLAY COMMANDS"
140 PRINT"  9) EDIT YOUR COMPOSITION"
145 PRINT"  0) TO MUSIC+"
150 PRINT@480," ";:A$=INKEY$:IFA$=""THEN150
155 IF A$="0" THEN 9000
160 B$=INKEY$:FORX=&H152 TO&H152+7:IFPEEK(X)=255THENNEXT:GOTO170:ELSE160
170 ONVAL(A$)GOTO300,310,320,330,340,360,440,8000,500
180 GOTO50
190 CLS:A$="c d z f g a z c d z f g a":B$="C D E F G A B C D E F G A B":FORX=1TOLEN(A$):C$=MID$(A$,X,1):IF C$="z"THENMID$(A$,X,1)=CHR$(175)
200 IFC$=" "THENMID$(A$,X,1)=CHR$(128)
210 NEXT:FORX=1 TOLEN(B$):C$=MID$(B$,X,1):IFC$=" "THENMID$(B$,X,1)=CHR$(138)
220 NEXT:CLS0
230 PRINT@4,A$;:PRINT@67,B$;:A$=CHR$(175)+CHR$(170)+CHR$(175)+CHR$(170)+A$:A$=LEFT$(A$,22):A$=A$+STRING$(4,255)+CHR$(128)+CHR$(255):PRINT@132,A$;:B$=STRING$(3,255)+CHR$(133)+B$:B$=LEFT$(B$,21):B$=B$+CHR$(128)+CHR$(255)+CHR$(128)+STRING$(3,255)
240 FOR X=1TOLEN(B$):IFMID$(B$,X,1)=CHR$(138)THENMID$(B$,X,1)=CHR$(133):NEXT :ELSE NEXT
250 PRINT@196,B$;:PRINT@267,STRING$(11,255);
260 PRINT@384,"ENTER = PAUSE : ? = NEW ENVELOP"
270 PRINT"SHIFT = MENU  : CLEAR=BACKSPACE"
280 PRINT"     SPACEBAR=REPEAT TOGGLE
290 RETURN
300 GOSUB190:POKE&H330D,&H33:POKE&H330E,&H4C:EXEC&H3000:GOTO50
310 GOSUB190:EXEC&H3000:GOTO50
320 GOSUB190:EXEC&H325A:GOTO50
330 CLS:S=&H3000:E=PEEK(&H330D)*256+PEEK(&H330E):PRINT"SAVE":INPUT"ENTER SONG NAME";F$:INPUT"(C)ASETTE OR (D)ISK";D$:IF D$="D" THEN SAVEM F$,S,E,&H325A:GOTO50
335 CSAVEMF$,S,E,&H325A:GOTO50
340 CLS:PRINT"LOAD:":INPUT"ENTER SONG NAME:";F$:INPUT"(C)ASETTE OR (D)ISK";D$:IFD$="C"THENCLOADMF$ :ELSELOADMF$
345 GOTO50
350 IFC=0THENRESET(A,B):RETURN:ELSESET(A,B,3):RETURN
360 CLS:INPUT"WHICH ENVELOPE DO YOU WANT TO    CHANGE (1 OR 2)";E
370 IFE=0THENE=1
380 IFE=1THENE=&H3219 :ELSEE=&H323A
390 CLS0
400 FORX=0TO63STEP2:A=PEEK(E):SET(X,31-INT(PEEK(E)/8),3):E=E+1:NEXT:E=E-32
410 Z=JOYSTK(0):A$=INKEY$:IFA$<>""THEN50:ELSEA=JOYSTK(2):A=INT(A/2):A=A*2:B=JOYSTK(3):B=INT(B/2):C=POINT(A,B):SET(A,B,3):FORX=1TO20:NEXT:GOSUB350:IFPEEK(&HFF00)=253THEN430
420 GOTO410
430 POKEE+A/2,255-(B*8):FORY=0 TO31:RESET(A,Y):NEXT:SET(A,B,3):GOTO410
440 PLYDLY=PEEK(&H32E2)*256+PEEK(&H32E3):ENVDLY=PEEK(&H317C)*256+PEEK(&H317D)
450 CLS:PRINT"CURRENT PLAYDELAY=";PLYDLY:PRINT"HIGHER OR LOWER VALUES WILL     LENGTHEN OR SHORTEN PAUSES BE-  TWEEN NOTES ON PLAYBACK ONLY.   ENTER NEW VALUE, OR <ENTER>     TO LEAVE THE SAME"
460 INPUTA:IFA<>0THENB=INT(A/256):C=A-256*B:POKE&H32E2,B:POKE&H32E3,C
470 CLS:PRINT"CURRENT ENVELOPE DELAY=";ENVDLY:PRINT"LOWER VALUES WILL COMPRESS THE  ENVELOPE INTO A SHORTER TIME    AND INCREASE THE TEMPO OF BOTH  COMPOSITION AND PLAYBACK. ENTER NEW VALUE OR <ENTER> TO LEAVE   THE SAME"
480 INPUTA:IFA<>0THENB=INT(A/256):C=A-256*B:POKE&H317C,B:POKE&H317D,C
490 GOTO50
500 CLS:S=&H334C:P=0:FS=0:CP=0:M=256*PEEK(&H330D)+PEEK(&H330E):LP=(M-S)/3:GOSUB510:GOTO520
510 N$(1)="C":N$(2)="C#":N$(3)="D":N$(4)="D#":N$(5)="E":N$(6)="F":N$(7)="F#":N$(8)="G":N$(9)="G#":N$(10)="A":N$(11)="A#":N$(12)="B":RETURN
520 FORX=0 TO448STEP32
530 IFP=LP THENFORZ=X TO448STEP32:PRINTSTRING$(16,CHR$(175)):NEXTZ:GOTO620
540 N=PEEK(S+3*P):IFN=255THENN$="PAU":PRINT@X,P:PRINT@X+5,N$:GOTO570
550 IFN=100THENN$="env":L1=0:L2=0:GOTO580
555 GOSUB560:GOTO570
560 O$=RIGHT$(STR$(INT((N-1)/12)+2),1):Z=N-12*INT((N-1)/12):N$=N$(Z):N$=O$+N$:RETURN
570 L1=PEEK(S+3*P+1):L2=PEEK(S+3*P+2)
580 PRINT@X,P:PRINT@X+5,N$:IFN=100THEN600
590 PRINT@X+10,RIGHT$(STR$(L1),2);L2
600 P=P+1
610 NEXT X
620 :'EDIT MENU
630 PRINT@17,"1.PLAY SEGMENT":PRINT@51,"TO CURSOR":PRINT@81,"2.PLAY START":PRINT@115,"TO CURSOR":PRINT@145,"3.PLAY CURSOR":PRINT@179,"TO END":PRINT@209,"4.NEW SEGMENT":PRINT@241,"5.CHANGE NOTE":PRINT@273,"6.INSERT NOTE":PRINT@305,"7.DELETE NOTE"
640 PRINT@337,"8.MAIN MENU":PRINT@369,"9.QUICK CHANGE":PRINT@403,"TEMPO BYTES"
650 X=4:P=FS:CP=0
660 PRINT@X,CHR$(128);
670 A$=INKEY$:IFA$=""THEN670
680 IFA$=CHR$(94)OR A$=CHR$(10)THEN720
690 IFVAL(A$)<1OR VAL(A$)>9 THEN 670
700 ONVAL(A$)GOTO790,800,810,820,845,948,980,710,1000
710 GOTO50
720 IFA$=CHR$(10)THEN740
730 IFX=4THEN670:ELSEPRINT@X," ";:X=X-32:CP=CP-1:GOTO660
740 IFX=452OR FS+CP+1=LP THEN670:ELSEPRINT@X," ";:X=X+32:CP=CP+1:GOTO660
750 M1=PEEK(&H330D):M2=PEEK(&H330E):RETURN
760 A1=INT((S+3*FS)/256):A2=(S+3*FS)-A1*256:RETURN
770 A1=INT((S+3*FS+3*(CP+1))/256):A2=(S+3*FS+3*(CP+1))-A1*256:RETURN
780 EXEC&H325A:FORZ=1 TO300:NEXTZ:PRINT@X,CHR$(255);:FORZ=1TO255STEP20:SOUNDZ,1:NEXT:POKE&H3262,&H33:POKE&H3263,&H4C:POKE&H330D,M1:POKE&H330E,M2:GOTO660
790 GOSUB750:GOSUB760:POKE&H3262,A1:POKE&H3263,A2:GOSUB770:POKE&H330D,A1:POKE&H330E,A2:GOTO780
800 GOSUB750:GOSUB770:POKE&H330D,A1:POKE&H330E,A2:GOTO780
810 GOSUB750:GOSUB770:Z=256*A1+A2:Z=Z-3:A1=INT(Z/256):A2=Z-A1*256:POKE&H3262,A1:POKE&H3263,A2:GOTO 780
820 CLS:PRINT"PLEASE TYPE POSITION NUMBER FOR NEW SEGMENT":INPUTQ
840 FS=Q:P=Q:GOTO520
845 GOSUB850:GOTO520
850 FOR Z=16TO464STEP32:PRINT@Z,STRING$(15," "):NEXTZ:Z=S+3*FS+3*CP:PRINT@17,"ENTER NEW NOTE":PRINT@49,"ENTER TO EXIT":PRINT@81,"";:INPUTN$
860 IFN$=""THENP=FS:RETURN
865 IF N$="ENV"THENPOKEZ,100:POKEZ+1,0:POKEZ+2,0:RETURN
875 IF N$="PAU"THENPOKEZ,255:GOTO930
890 PRINT@113,"OCTAVE?":PRINT@145,"";:INPUTO:IFO<2ORO>4THEN890:ELSEFORX=1 TO12:IFN$<>N$(X)THENNEXT:ELSEN=(O-2)*12+X:POKEZ,N:GOTO930
892 IF N$="PAU"THENPOKEZ,255:GOTO930
894 IF N$="ENV"THENPOKEZ,100:GOTO930
900 GOTO845
930 PRINT@177,"NOTE LEN(1-32)":PRINT@209,"";:INPUTN:IF N>32OR N<1 THEN 930
935 POKEZ+1,N
940 PRINT@241,"PAUSE? (1-255)":PRINT@273,"";:INPUTN:IFN<1ORN>255THEN940
945 POKEZ+2,N:P=FS
947 RETURN
948 M=M+3:GOSUB950:GOTO960
950 PRINT@433,"thinking":M1=INT(M/256):M2=M-M1*256:POKE&H330D,M1:POKE&H330E,M2:RETURN
960 FOR Z=M TOS+3*FS+3*CP+1STEP-1:A=PEEK(Z-1):POKEZ+2,A:NEXT
970 GOSUB850:LP=(M-S)/3:GOTO520
980 M=M-3:GOSUB950:FORZ=S+3*FS+3*CP TOM+2:A=PEEK(Z+3):POKEZ,A:NEXT:LP=(M-S)/3:GOTO520
1000 :CLS:PRINT"THIS OPTION CHANGES ALL NOTE    LENGHTS AND/OR PAUASE BYTES     FROM BEGINING OF SEGMENT TO THE CURSOR":PRINT:PRINT"ENTER NOTE  LENGTH (1-32)       <ENTER> TO LEAVE  UNCHANGED":INPUTA:IFA=0THEN1030:ELSEIFA<1ORA>32THEN1000
1010 FORZ=S+3*FS TOS+3*FS+3*CP STEP3:POKEZ+1,A:NEXT
1030 PRINT:PRINT"ENTER NOTE PAUSE <ENTER> TO     ESCAPE":INPUTA:IF A=0THEN520:ELSEIFA<1OR A>255THEN1030
1040 FORZ=S+3*FS TOS+3*FS+3*CP STEP3:POKEZ+2,A:NEXT:GOTO520
8000 :'PLAY STATEMENTS
8002 L=0:I=0:A=&H334C:E=PEEK(&H330D)*256+PEEK(&H330E):GOSUB510
8010 D=1:CLS:PRINT"PREPARE TAPE RECORDER OR DISK   TO RECEIVE PLAY STATEMENTS":PRINT:INPUT"FILENAME";F$:INPUT"(C)ASSETTE OR (D)ISK";D$:IFD$="C"THENOPEN"O",#-1,F$:D=-1:GOTO8012
8011 F$=F$+"/BAS":OPEN"O",#1,F$
8012 B$="5 PLAY"+CHR$(34)+"T6"+CHR$(34):PRINT#D,B$
8014 L=L+10:L$=STR$(L):L$=RIGHT$(L$,LEN(L$)-1):A$=L$+" PLAY"+CHR$(34)
8020 FORX=A TOA+27STEP3:IFX=E THEN8100
8030 N=PEEK(X):IFN=255THEN8040:ELSEIFN=100THEN8160:ELSEGOSUB560:N$="O"+N$:IFRIGHT$(N$,1)="#"THENN$=LEFT$(N$,LEN(N$)-1):N$=N$+"+"
8040 L1=PEEK(X+1):L2=PEEK(X+2):IFL1=0THENL1=1:IFL2=0THENL2=1
8050 IF L1>16ANDL1<20THENL1=16:ELSE IF L1>19ANDL1<28THENL1=3:GOTO8060
8055 L1=INT(1/(L1/32))
8060 IF L2>128ANDL2<160 THEN L2=127 :ELSEIFL2>127ANDL2<224 THEN L2=3:GOTO8070
8065 L2=INT(1/(L2/255))
8070 L1$=STR$(L1):L2$=STR$(L2):L1$=RIGHT$(L1$,LEN(L1$)-1):L2$=RIGHT$(L2$,LEN(L2$)-1)
8075 IF N=255THEN8180
8080 N$="L"+L1$+N$+"P"+L2$
8090 A$=A$+N$:NEXTX
8100 A$=A$+CHR$(34):PRINT#D,A$:PRINTA$
8114 IFX>=E THENCLOSE#D:GOTO50
8120 A=A+30:GOTO8014
8160 IFX=E THEN50:ELSENEXTX
8180 N$="P"+L1$+"P"+L2$:GOTO8090
9000 :'MUSIC+
9002 L=0:I=0:A=&H334C:E=PEEK(&H330D)*256+PEEK(&H330E):GOSUB 510
9010 INPUT "MUSIC+ NAME";N$
9020 OPEN "O",#1,N$
9030 FOR X=A TO E STEP 3
9040 PRINT #1,PEEK(X+1),PEEK(X)*2
9050 IF PEEK(X+2)/6 > 1 THEN PRINT #1,INT(PEEK(X+2)/6),0
9060 NEXT X
9070 CLOSE:GOTO 50
20000 PCLEAR1:GOTO10
