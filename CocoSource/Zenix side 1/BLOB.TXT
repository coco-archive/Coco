BLOBBLOW
       FDB   +4*256
       FDB   +10*256-6
       FDB   +10*256+6
       FDB   +14*256-3
       FDB   +14*256+3
       FDB   +20*256-7
       FDB   +20*256+7
       FDB   +20*256
       FDB   +26*256-3
       FDB   +26*256+3
       FDB   +30*256+6
       FDB   +30*256-6
       FDB   +36*256
       FDB   0

C@     PULS  D,Y,PC
BLOB   LDA   OBNUM
       CMPA  #15
       BHS   C@
       LDD   #D@
       STD   TRKDRV,Y
       LDD   #$1000
       STD   NEWPOS,Y
       LDD   #BLOBHIT
       STD   SPCDRV,Y
       LDA   #%10010001
       STA   STATUS,Y
A@     JSR   DRAGSPOT
       JSR   ILINE
D@     LDA   SHIPOS
       SUBA  NEWPOS,Y
       ADDA  #17
       CMPA  #36+17-4
       BHI   N@
       LDA   SHIPOS+1
       SUBA  NEWPOS+1,Y
       ADDA  #10
       CMPA  #10*2+1
       BHI   N@
       CLR   SHIELD
       CLR   SHLD
       BRA   BLOBDIE
N@     DEC   DRAGHITINC,Y
       BNE   U@
       LDA   #96
       STA   DRAGHITINC,Y
       INC   DRAGHITS,Y
       BNE   U@
       DEC   DRAGHITS,Y
U@     JSR   DLINE
       JSR   DLINE
       BNE   DRAW@
       BRA   A@
DRAW@  JMP   DRAWBLOB

BLOBHIT
       BULHIT 40,11,23
       DEC   DRAGHITS,U
       BNE   ?RTS
       LDD   #A@
       STD   TRKDRV,U
       RTS
BLOBDIE
A@     JSR   EXPLODE
       LDA   #128
       STA   EXPLEN
       ADDSCR #250
       ADDSCR #250
       LDX   #BLOBBLOW
B@     LDD   ,X++
       BEQ   C@
       ADDD  NEWPOS,Y
       SUBA  #5
       JSR   MBLOW
       BRA   B@
C@     LDA   #30
       STA   IWAIT2,Y
       NXTFRM NOPRN
       DEC   IWAIT2,Y
       BEQ   D@
       PULS  Y,D,PC
D@     JMP   TRKEND

X@     PSHS  Y,D
DRAWBLOB
       LDA   NEWPOS,Y
       LDB   #58
       MUL
       SUBA  HLINE
       ADDA  #6
       CMPA  #15
       BHI   A@
*PUT ON FIFO
       LDX   FIFOP
       LDY   +2,S
       STY   ,X++
       LDD   #X@
       STD   ,X++
       STX   FIFOP
       PULS  Y,D,PC
*DRAW BLOB
A@     LDX   #BIGBLOCK
       LDA   NEWPOS,Y
       ANDA  #%11100000
       LSRA
       LSRA
       LSRA
*PLACE IN MEMORY
       LDU   A,X
       STU   $FFA3
       ADDA  #2
       LDB   A,X
       STB   $FFA5
       LDD   NEWPOS,Y
       ANDA  #%00011111
       ADDA  #$60
       TFR   D,U
       JSR   DRBLOB
       PULS  Y,D,PC
