SR     EQU   0 *STEP RATE

TIMER  EQU   $985
IOTEST EQU   1
RAMIM  EQU   $986

       ORG   $EA
DSKOP  RMB   1
DSKDRV RMB   1
DSKTRK RMB   1
DSKSEC RMB   1
DSKBUF RMB   2
DSKSTA RMB   1
 MSG "DSKSTA = $",DSKSTA
       RMB   2 *NO US  FCB   0
MOFF   RMB   1
REALTRACK RMB 1
SLOW   RMB    1

       ORG   $7000

BUFFERW BSZ  300
BUFFERR BSZ  300
BUFFERW2 BSZ 300

READY  EXG   A,A
       EXG   A,A
       LDY   #0
R@     LEAY  -1,Y
       BEQ   FRCINT
       LDA   $FF48
       BITA  #1
       BNE   R@
       RTS
FRCINT LDA   #$D0
       STA   $FF48
       EXG   A,A
       EXG   A,A
       LDA   $FF48
       LDA   #$80
       STA   DSKSTA
       RTS

VTRACK LDD   #$600
       STD   DSKBUF
       LDA   #2 *READ
       STA   DSKOP
       LDU   #SECLIST
B@     LDA   ,U+
A@     STA   DSKSEC
       JSR   [$C004]
       LDA   DSKSTA
       BNE   Z@
       LDA   ,U+
       BNE   A@
Z@     RTS

DSKERR LDA   $F0
       BEQ   ?RTS
       LDA   #'E
       JSR   [$A002]
       BRA   DSKERR



SECLIST FCB 1,2,3,4,5,6
 FCB 7,8,9,10,11,10,12,13,14
 FCB 15,16,17,0


DSKINI
       LDA   #'W
       JSR   [$A002]
       LDB   #2
       STB   DSKDRV
D@     COM   $95C
       LDS   #$800
       CLR   DSKTRK
*LOOP BACK HERE
F@     LDA   #4 *SEEKIT
       STA   DSKOP
       JSR   [$C004] *SEEK
       CLR   65496
       CLR   $985
       LDA   RAMIM
       STA   $FF40
       CLR   65497
       JSR   BUILD
       CLR   65496
       LDY   #$FF4B
       ORCC  #$50
       LDX   #DSNNMI
       STX   $983
       LDX   #$989
       LDA   $FF48
       LDA   #$FF
       STA   $982
       LDB   #$F4
       STB   $FF48
       LDA   RAMIM
       ORA   #$80
       STA   $FF40
L@     LDB   ,X+
       STB   ,Y
       BRA   L@
DSNNMI LDA   $FF48
       ANDCC #$AF
       ANDA  #$44
       STA   DSKSTA
       BNE   ERROR@
*COPY PROTECTION
       LDA   #3
       LDX   #BUFFERW
       BSR   PUTBUFFER
       LDA   #1
       LDX   #BUFFERW2
       BSR   PUTBUFFER
       LDA   #2 *READ
       STA   $EA
       LDA   #8  *SECTOR
       STA   $ED
       LDD   #BUFFERR *BUFFER
       STD   $EE
       JSR   [$C004]
       LDA   #3 *WRITE
       STA   $EA
       LDA   #10 *SECTOR
       STA   $ED
       LDD   #BUFFERW
       STD   $EE *BUFFER
       JSR   [$C004]
       LDD   #BUFFERW2
       STD   $EE
       JSR   [$C004]
       JSR   VTRACK
       JSR   DSKERR
       INC   DSKTRK
       LDA   DSKTRK
       CMPA  #35
      LBLO   F@
       JMP   $D2CD
ERROR@ JMP   $A619

*ON ENTRY X ^ SECTOR
*A = SECTOR DATA
PUTBUFFER
       CLRB
A@     ANDA  #$07
       STA   ,X+
       INCA
       DECB
       BNE   A@
       RTS

BUILD  LDX   #$989
       LDD   #$204E
       BSR   WAB
       CLRB
A@     PSHS  B
       LDU   #SECLIST
       LDB   B,U
       STB   $ED
       LDU   #FORMAT
E@     LDB   ,U+
       BSR   WAX
       LDA   REALTRACK
       STA   ,X+
*WHICH SIDE?
       LDA   RAMIM
       ROLA
       ROLA
       ROLA
       ANDA  #1
       STA   ,X+
       LDA   $ED
       STA   ,X+
       LDB   ,U+
       BSR   WAX
       PULS  B
       INCB
       CMPB  #18
       BLO   A@
       LDD   #$C84E
WAB    STB   ,X+
       DECA
       BNE   WAB
       RTS
WAX    PSHS  B
       LDD   ,U++
       BSR   WAB
       PULS  B
       DECB
       BNE   WAX
       RTS


FORMAT
       FCB   3 *DO 3
       FCB   8,0
       FCB   3,$F5
       FCB   1,$FE
*TRACK, SIDE, SECTOR
       FCB  9 *DO 10
       FCB   1,1
       FCB   1,$F7
       FCB   22,$4E
       FCB   12,0
       FCB   3,$F5
       FCB   1,$FB

       FCB   0,$FF

       FCB   1,$F7
       FCB   24,$4E

       END   DSKINI
