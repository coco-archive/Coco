       ORG   $8DB0
       JMP   $8D7C

       ORG   $8D7C
       LDD   #$7E04
       STD   $8DB0
       CLR   $8DB2
       LDX   #$400
       JMP   $8DA5

       ORG   $E00
*ENTRY:B=BYTE OUT
*      RS232 = 1
BOUT   ORCC  #$50
       PSHS  B
       LDX   #$FF20
       LDA   ,X
       EORA  #2
       STA   ,X  *4
       LDB   #8
R@     LSR   ,S  *6
       BCS   A@  *3
       ANDA  #%11111101
       BRA   C@  *3
A@     ORA   #2
       BRN   A@
C@     STA   ,X  *4
       DECB      *2
       BNE   R@  *3
       EXG   A,A
       EXG   A,A
       EXG   A,A
       ORA   #2
       STA   ,X
       PULS  B,PC

CIN     EQU   $8DBC
COUT    EQU   $8E0C
MESS1  FCB   13,13,13,13
 FCC "ON THE OTHE COMPUTER,",13
 FCC "TYPE THE FOLLOWING:",13,13
 FCC "POKE &HE6,4",13
 FCC "EXEC &H8D7C",13,0

PRINT  LDA   ,X+
       JSR   [$A002]
       TSTA
       BNE   PRINT
       RTS

EXEC   LDX   #MESS1
       JSR   PRINT
       ORCC  #$50
       LDB   #4
       STB   $E6
       CLR   65496
       JSR   CIN  *COMMAND
       STA   $400
       EXG   A,A
       JSR   COUT *ECHO
       EXG   A,A
       JSR   CIN  *BLOCK #
       EXG   A,A
       JSR   CIN  *BLOCK #
       EXG   A,A
       JSR   CIN  *CHECK
       EXG   A,A
       LDA   #$C8
       JSR   COUT *ACK
       EXG   A,A
       JSR   COUT *CHAR COUNT
       EXG   A,A
       LDB   #128
       LDX   #$8D7C
A@     LDA   ,X+
       JSR   COUT
       DECB
       BNE   A@
       LDX   #CODE
       LDB   #128
B@     LDA   ,X+
       JSR   COUT
       DECB
       BNE   B@
       ORCC  #$50
       CLR   65497
       LDA   $FF20
       ORA   #2
       STA   $FF20
       LDX   #0
T@     LEAX  -1,X
       BNE   T@
*SEND DATA TO OTHER COMPUTER
       LDU   #$C000
C@     INC   $400
       LDB   ,U+
       JSR   BOUT
       EXG   A,A
       CMPU  #$E000
       BLO   C@
*PATCH FOR DSKON
       LDU   #PATCH
D@     COM   $400
       LDB   ,U+
       EXG   A,A
       JSR   BOUT
       CMPU  #PATCH+256
       BLO   D@
*ACT AS HOST COMPUTER

       LDX   #$400
       STX   $EE
       LDD   #'O*256+'K
       STD   $400
       STD   $500
HOST   CLR   65497
       JSR   BIN *READ/WRITE
       CMPB  #2
       BEQ   XXX
       CMPB  #3
       BNE   HOST
XXX    STB   $EA
       JSR   BIN *DRIVE
       CMPB  #3
       BHI   HOST
       STB   $EB
       JSR   BIN *TRACK
       CMPB  #35
       BHI   HOST
       STB   $EC
       JSR   BIN *SECTOR
       TSTB
       BEQ   HOST
       CMPB  #18
       BHI   HOST
       STB   $ED
       LDU   #$400
       LEAU  +256,U
       PSHS  U
       LDB   $EA
       CMPB  #2
       BEQ   READ
*THIS COMPUTER READS DATA
WRITE  LDU   #$400
A@     JSR   BIN
       STB   ,Y+
       CMPU  ,S
       BLO   A@
       JSR   [$C004]
       ORCC  #$50
       CLR   65497
STAT   LEAS  +2,S
       EXG   A,A
       LDB   $F0
       JSR   BOUT
       BRA   HOST
READ   JSR   [$C004]
       ORCC  #$50
       CLR   65497
       LDU   #$400
B@     LDB   ,U+
       JSR   BOUT
       CMPU  ,S
       BLO   B@
       BRA   STAT


*FOLLOWING CODE IS AT $400
CODE   ORCC  #$50
       CLR   65497 *FAST
       LDU   #$C000
A@     BSR   BIN
       STB   ,U+
       INC   $400
       CMPU  #$E000
       BLO   A@
       CLR   113
       LDU   #$D75F
*PATCH DSKON
B@     COM   $400
       BSR   BIN
       STB   ,U+
       CMPU  #$D85F
       BLO   B@
       JMP   $A027

*ENTRY B=BYTEIN
BIN    ORCC  #$50
       PSHS  B
       LDX   #$FF22
       LDA   #1
A@     BITA  ,X
       BNE   A@
       LDB   #8
       TFR   A,A
       BRN   A@
       NOP
       BRN   A@
       EXG   A,A
B@     LDA   ,X *4
       LSRA     *2
       ROR   ,S *6
       TFR   A,A *6
       DECB     *2
       BNE   B@ *3
       PULS  B,PC

PATCH  PSHS  X,Y,U,D,CC
       ORCC  #$50
       CLR   65497
       LDB   $EA
       LBSR  OUT
       EXG   A,A
       EXG   A,A
       LDB   $EB
       LBSR  OUT
       EXG   A,A
       EXG   A,A
       LDB   $EC
       LBSR  OUT
       EXG   A,A
       EXG   A,A
       LDB   $ED
       LBSR  OUT
       EXG   A,A
       EXG   A,A
       LDU   $EE
       LEAU  +256,U
       PSHS  U
       LDU   $EE
       LDB   $EA
       CMPB  #2
       BEQ   DREAD
DWRITE LDB   ,U+
       LBSR  OUT
       CMPU  ,S
       BLO   DWRITE
DSTAT  BSR   IN
       STB   $F0
       LEAS  +2,S
       PULS  X,Y,U,D,CC,PC
DREAD  BSR   IN
       STB   ,U+
       CMPU  ,S
       BLO   DREAD
       BRA   DSTAT

*ENTRY:B=BYTE OUT
*      RS232 = 1
OUT   ORCC  #$50
       PSHS  B
       LDX   #$FF20
       LDA   ,X
       EORA  #2
       STA   ,X  *4
       LDB   #8
R@     LSR   ,S  *6
       BCS   A@  *3
       ANDA  #%11111101
       BRA   C@  *3
A@     ORA   #2
       BRN   A@
C@     STA   ,X  *4
       DECB      *2
       BNE   R@  *3
       EXG   A,A
       EXG   A,A
       EXG   A,A
       ORA   #2
       STA   ,X
       PULS  B,PC

*ENTRY B=BYTEIN
IN    ORCC  #$50
       PSHS  B
       LDX   #$FF22
       LDA   #1
A@     BITA  ,X
       BNE   A@
       LDB   #8
       TFR   A,A
       BRN   A@
       NOP
       BRN   A@
       EXG   A,A
B@     LDA   ,X *4
       LSRA     *2
       ROR   ,S *6
       TFR   A,A *6
       DECB     *2
       BNE   B@ *3
ENDER  PULS  B,PC

 MSG ENDER-PATCH

       END   EXEC
