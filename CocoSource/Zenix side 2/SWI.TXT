       LBRA  PR1CH
       LBRA  SWIEX

CHMODE EQU   128
GOTOXY EQU   129
CHCOL  EQU   130

LETTER
 FCB 0,0,0,0,0,0,0,0
 FCB 12,12,12,12,12,0,12,0
 FCB 102,102,102,0,0,0,0,0
 FCB 102,102,255,102,255,102,102,0
 FCB 12,31,44,30,13,62,12,0
 FCB 56,59,6,12,24,55,7,0
 FCB 24,60,60,24,63,54,31,0
 FCB 12,12,24,0,0,0,0,0
 FCB 6,12,24,24,24,12,6,0
 FCB 24,12,6,6,6,12,24,0
 FCB 8,42,28,127,28,42,8,0
 FCB 0,12,12,63,12,12,0,0
 FCB 0,0,0,0,0,12,12,24
 FCB 0,0,0,63,0,0,0,0
 FCB 0,0,0,0,0,0,12,0
 FCB 0,3,6,12,24,48,0,0
*NUMBERS (0, 1, 2, 3...0)
 FCB 30,51,55,63,59,51,30,0
 FCB 12,28,12,12,12,12,30,0
 FCB 30,51,3,30,48,48,63,0
 FCB 30,51,3,6,3,51,30,0
 FCB 6,14,30,54,63,6,6,0
 FCB 63,48,62,3,3,51,30,0
 FCB 30,48,48,62,51,51,30,0
 FCB 63,3,6,12,24,48,48,0
 FCB 30,51,51,30,51,51,30,0
 FCB 30,51,51,30,3,3,30,0
 FCB 0,0,48,0,0,48,0,0
 FCB 0,0,12,0,0,12,12,24
 FCB 6,12,24,48,24,12,6,0
 FCB 0,0,63,0,63,0,0,0
 FCB 24,12,6,3,6,12,24,0
 FCB 30,51,3,6,12,0,12,0
*LETTERS (@, A, B, C, D...)
 FCB 30,51,3,31,55,55,30,0
 FCB 12,30,51,51,63,51,51,0
 FCB 62,51,51,62,51,51,62,0
 FCB 30,51,48,48,48,51,30,0
 FCB 62,51,51,51,51,51,62,0
 FCB 63,48,48,60,48,48,63,0
 FCB 63,48,48,60,48,48,48,0
 FCB 30,51,48,48,55,51,30,0
 FCB 51,51,51,63,51,51,51,0
 FCB 30,12,12,12,12,12,30,0
 FCB 3,3,3,3,3,51,30,0
 FCB 51,54,60,56,60,54,51,0
 FCB 48,48,48,48,48,48,63,0
 FCB 33,51,63,63,51,51,51,0
 FCB 51,51,59,63,55,51,51,0
 FCB 30,51,51,51,51,51,30,0
 FCB 62,51,51,62,48,48,48,0
 FCB 30,51,51,51,63,54,31,0
 FCB 62,51,51,62,60,54,51,0
 FCB 30,51,48,30,3,51,30,0
 FCB 63,12,12,12,12,12,12,0
 FCB 51,51,51,51,51,51,30,0
 FCB 51,51,51,30,30,12,12,0
 FCB 51,51,51,63,63,51,33,0
 FCB 51,51,30,12,30,51,51,0
 FCB 51,51,30,12,12,12,12,0
 FCB 63,3,6,12,24,48,63,0
 FCB 30,24,24,24,24,24,30,0
 FCB 0,48,24,12,6,3,0,0
 FCB 30,6,6,6,6,6,30,0
 FCB 12,30,63,12,12,12,12,0
 FCB 0,12,24,63,24,12,0,0
 FCB 12,30,51,0,0,0,0,0
 FCB 0,0,30,3,31,51,31,0
 FCB 48,48,62,59,51,59,62,0
 FCB 0,0,30,51,48,51,30,0
 FCB 3,3,31,55,51,55,31,0
 FCB 0,0,30,51,63,48,30,0
 FCB 6,15,12,30,12,12,12,0
 FCB 0,0,31,51,51,31,3,30
 FCB 48,48,62,59,51,51,51,0
 FCB 0,12,0,28,12,12,30,0
 FCB 0,3,0,3,3,3,51,30
 FCB 48,48,54,60,56,60,54,0
 FCB 28,12,12,12,12,12,30,0
 FCB 0,0,126,107,107,107,107,0
 FCB 0,0,62,59,51,51,51,0
 FCB 0,0,30,51,51,51,30,0
 FCB 0,0,62,51,51,62,48,48
 FCB 0,0,31,51,51,31,3,3
 FCB 0,0,62,59,48,48,48,0
 FCB 0,0,31,48,30,3,62,0
 FCB 24,24,60,24,24,27,14,0
 FCB 0,0,51,51,51,55,31,0
 FCB 0,0,51,51,51,30,12,0
 FCB 0,0,99,99,107,119,34,0
 FCB 0,0,51,30,12,30,51,0
 FCB 0,0,51,51,51,31,3,30
 FCB 0,0,63,6,12,24,63,0
 FCB 6,12,12,24,12,12,6,0
 FCB 12,12,12,0,12,12,12,0
 FCB 24,12,12,6,12,12,24,0
 FCB 24,63,6,0,0,0,0,0
 FDB $FFFF,$FFFF,$FFFF,$FFFF

GRBLK2
 FDB $3536,$3637,$3738,$3839
 FDB $393A,$3A3B,$3B3C,$3C35

*Y ^ SCREEN POS
*A = COLOR
*B = CHR TO PRINT
CHCLTB BSZ   4
CHACRS FDB   0
CHBLK  FDB   0
CHCUR  FDB   0
CHRB   PSHS  X,Y,U,D
       LSLB
       LEAX  LETTER,PCR
       ABX
       ABX
       ABX
       ABX
       LDD   CHBLK,PCR
       STD   $FFA1
       LDY   CHCUR,PCR
       LEAU  CHCLTB,PCR
       LDA   ,U
       LDB   ,U
       STD   ,Y *1ST LINE
       STD   +2,Y
       LDD   CHACRS,PCR
       LEAY  D,Y
       LDB   #8
       PSHS  B
A@     LDB   ,X+
       LSLB
       TFR   B,A
       ANDA  #$3
       LDA   A,U
       STA   +3,Y
       LSRB
       LSRB
       TFR   B,A
       ANDA  #$3
       LDA   A,U
       STA   +2,Y
       LSRB
       LSRB
       TFR   B,A
       ANDA  #$3
       LDA   A,U
       STA   +1,Y
       LSRB
       LSRB
       LDB   B,U
       STB   ,Y
       LDD   CHACRS,PCR
       LEAY  D,Y
       DEC   ,S
       BNE   A@
       LEAS  +1,S
       LDD   CHCUR,PCR
       ADDD  #4
       STD   CHCUR,PCR
       PULS  X,Y,U,D,PC

COLORTABLE
 FDB $0000,$000F,$00F0,$00FF
 FDB $0F00,$0F0F,$0FF0,$0FFF
 FDB $F000,$F00F,$F0F0,$F0FF
 FDB $FF00,$FF0F,$FFF0,$FFFF

*B = CHR
*Y ^ PLACE TO PRINT
PR1CH  PSHS  X,Y,U,D
       LEAX  LETTER,PCR
       SUBB  #32
       LDA   #8
       PSHS  A
       MUL
       LEAX  D,X
       LEAU  COLORTABLE,PCR
       LDD   ,U
       STD   ,Y
       STD   +2,Y
A@     LEAY  +256,Y
       LDB   ,X
       ANDB  #%11110000
       LSRB
       LSRB
       LSRB
       LDD   B,U
       STD   ,Y
       LDB   ,X+
       ANDB  #%00001111
       LSLB
       LDD   B,U
       STD   +2,Y
       DEC   ,S
       BNE   A@
       LEAS  +1,S
       PULS  X,Y,U,D,PC

*U ^ VARS ON STACK
*X = PC
SWIEX  CMPB  #10
       BLS   A@
       RTS
A@     LEAY  <SWITAB,PCR
       LSLB
       LSLB
       JMP   B,Y
SWITAB LBRA  WRTLN
       NOP
       LBRA  PSTRN
       NOP
       LBRA  WRTLX
       NOP
       LBRA  DBCD
       NOP
       LBRA  SGOTOX
       NOP
       LBRA  SWRCLR
       NOP
       LBRA  SGKEY
       NOP
       LBRA  WRTCHA

WRTLN  BSR   WRTLX2
       STX   10,U
       RTS

WRTLX  LDX   4,U
WRTLX2 LDB   ,X+
       BEQ   ?RTS
       BMI   A@ *BRIF ESC
       SUBB  #32
       BCC   Z@
       ADDB  #32
       CMPB  #8
       BNE   WRTLX2
       LDD   CHCUR,PCR
       SUBD  #4
       STD   CHCUR,PCR
       CLRB
      LBSR   CHRB
       LDD   CHCUR,PCR
       SUBD  #4
       STD   CHCUR,PCR
       BRA   WRTLX2
Z@     CMPB  #95
       BHI   WRTLX2
       LBSR  CHRB
       BRA   WRTLX2
A@     CMPB  #CHMODE
       BNE   B@
       LDD   ,X++
       STD   CHACRS,PCR
       BRA   WRTLX2
B@     CMPB  #GOTOXY
       BNE   C@
       LDD   ,X++
      LBSR   GXYCH
       BRA   WRTLX2
C@     CMPB  #CHCOL
       BNE   D@
       LDD   ,X++
      LBSR   SWRCL
D@     BRA   WRTLX2

DECMAL CLR   ,U
       CLR   +1,U
       CLR   +2,U
       CLR   +3,U
A@     INC   ,U
       SUBD  #10000
       BCC   A@
       ADDD  #10000
       DEC   ,U+
B@     INC   ,U
       SUBD  #1000
       BCC   B@
       ADDD  #1000
       DEC   ,U+
C@     INC   ,U
       SUBD  #100
       BCC   C@
       ADDD  #100
       DEC   ,U+
D@     INC   ,U
       SUBB  #10
       BCC   D@
       DEC   ,U+
       ADDB  #10
       STB   ,U
       RTS

PSTRN  LDD   1,U
       CLR   ,-S
       COM   ,S
       LEAS  -5,S
       LEAU  ,S
       BSR   DECMAL
A@     LDB   ,S+
       BEQ   A@
       BPL   B@
       CLRB
       STB   ,-S
       COM   ,S
B@     ADDB  #16
      LBSR   CHRB
       LDB   ,S+
       BPL   B@
       RTS

DBCD   LDB   1,U
       LSRB
       LSRB
       LSRB
       LSRB
       ADDB  #16
      LBSR   CHRB
       LDB   1,U
       ANDB  #$0F
       ADDB  #16
      LBSR   CHRB
       RTS

SGOTOX LDD   +1,U
GXYCH  LEAY  GRBLK2,PCR
       PSHS  D
       LSRA
       LSRA
       LSRA
       LSRA
       ANDA  #%00001110
       LDD   A,Y
       STD   CHBLK,PCR
       PULS  D
       ANDA  #%00011111
       ADDA  #$20
       STD   CHCUR,PCR
       RTS


SWRCLR LDD   1,U
SWRCL  LEAY  CHCLTB,PCR
       STA   ,Y
       STB   +3,Y
       PSHS  D
       ANDA  #$F0
       ANDB  #$0F
       PSHS  B
       ORA   ,S+
       STA   +1,Y
       PULS  D
       ANDA  #$0F
       ANDB  #$F0
       PSHS  B
       ORA   ,S+
       STA   +2,Y
       RTS

KEYBUF BSZ   8
SHIFT  FCB   0
CTRL   FCB   0
ALT    FCB   0
F1     FCB   0
F2     FCB   0

SCNASC CMPA  #26
       BHI   A@
*CONVERT TO LOWER CASE LETTER
       TSTA
       BNE   E@
       LDA   #64 *THE @ (AT)
       RTS
E@     TST   SHIFT,PCR
       BEQ   G@
       ADDA  #96
       RTS
G@     ADDA  #64
       RTS
A@     CMPA  #32
       BLO   S@ *SPECIAL KEY
       CMPA  #48
       BLO   N@ *NUMBER
*CONTROLE CHAR
       CMPA  #50
       BLS   C@
*CTRL, ALT, SHIFT, F1, F2
       CLRA
       RTS
C@     SUBA  #16
S@     SUBA  #27
       LSLA
       PSHS  X,B
       LEAX  Z@,PCR
       LEAX  A,X
       LDA   ,X
       LDB   SHIFT,PCR
       BNE   D@
       LDA   +1,X
D@     PULS  B,X,PC
N@     CMPA  #44
       BHS   I@
       ADDA  #16
I@     TST   SHIFT,PCR
       BNE   ?RTS
       EORA  #16
       RTS
Z@     FCB   94,95 *UP ARR ^
       FCB   10,91 *DWN ARR
       FCB   8,21 *LFT ARR
       FCB   9,93 *RGHT ARR
       FCB   32,32 *SPACE
       FCB   13,13 *ENTER
       FCB   12,92 *CLEAR
       FCB   3,3  *BREAK

SGKEY  PSHS  U
       BSR   GETKEY
       TFR   CC,B
       PULS  U
       STA   1,U
       LDA   ,U
       ANDA  #%11111011
       ANDB  #%00000100
       STA   ,U
       ORB   ,U
       STB   ,U
       RTS

GETKEY BSR   GETSCN *GET SCAN
       BPL   A@
       CLRA
       RTS
A@     BSR   CNTRL
       BSR   SCNASC *CVT TO ASC
       TSTA
       RTS

CNTRL  PSHS  A
       LDA   KEYBUF,PCR
       ANDA  #%01000000
       STA   SHIFT,PCR
       LDA   KEYBUF+4,PCR
       ANDA  #%01000000
       STA   ALT,PCR
       LDA   KEYBUF+3,PCR
       ANDA  #%01000000
       STA   CTRL,PCR
       LDA   KEYBUF+2,PCR
       ANDA  #%01000000
       STA   F1,PCR
       LDA   KEYBUF+1,PCR
       ANDA  #%01000000
       STA   F2,PCR
       PULS  A,PC

GETSCN PSHS  X,B
       LDA   #$FF
       STA   $FF02
       LEAX  KEYBUF,PCR
       LDB   #8
       CLR   ,-S  *VARS ON
       CLR   ,-S  *STACK
       LEAS  -1,S *"""""""
       ANDCC #$FE
*POLE FROM COLUMN 8 TO 1
*B=COLUMN COUNTER, A=ROW
A@     ROR   $FF02 *COLUMN
       LDA   $FF00 *ROW
       ORA   #$80
       STA   ,S *SAVE ROW
       EORA  ,X
       ANDA  ,X
       BEQ  B@ *BRIF NO NEW KEY
*NEW KEY IS PRESSED IN THIS ROW
*SAVE KEY INFO
       STA   +1,S *SAVE ROW
       STB   +2,S *SAVE COLUMN
B@     LDA   ,S  *GET ROW AND
       STA   ,X+ *SAVE IT
       DECB      *NEXT COLUMN
       BNE   A@ *BRIF MORE
*FINISHED WITH POLE
       CLRA
       LDB   +1,S *ROW
       BNE   C@ *BRIF KEY FOUND
       LEAS  +3,S
       COMA
       PULS  X,B,PC
C@     INCA
       LSRB
       BCC   C@
       DECA
       LSLA
       LSLA  *MULT A BY 8
       LSLA
       ADDA  +2,S
       DECA
*A=KEYBOARD SCAN CODE
       LEAS  +3,S
       PULS  X,B,PC

WRTCHA PSHS  D
       LDB   1,U
       BMI   A@
       LEAX  ,S
       STB   ,X
       CLR   +1,X
      LBSR   WRTLX2
A@     PULS  D,PC
 MSG "END SWI @",*
